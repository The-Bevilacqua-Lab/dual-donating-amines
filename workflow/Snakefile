import scripts.parse_nrlist

configfile: "config/config.yaml"

# Prepare a list of strings where each string includes the equivalence class name, PDB ID, and chain info for the
# equivalence class members.
eq_class_members = scripts.parse_nrlist.parse_nrlist(config["rep_set_file"], config["all_members"])

rule all:
    input:
        "analysis/atom_pairs.csv",
        "analysis/counts.csv"

rule collect_data:
    output:
        counts = "data/counts/{eq_class_members}_counts.csv",
        h_bond = "data/h_bond/{eq_class_members}_h_bond.csv",
        nuc = "data/nuc/{eq_class_members}_nuc.csv",
        b_factor = "data/b_factor/{eq_class_members}_b_factor.csv"
    log:
        stdout = "data/logs/collect_data/{eq_class_members}.stdout",
        stderr = "data/logs/collect_data/{eq_class_members}.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/collect_data.py"

rule process_data:
    input:
        counts = "data/counts/{eq_class_members}_counts.csv",
        h_bond = "data/h_bond/{eq_class_members}_h_bond.csv",
        nuc = "data/nuc/{eq_class_members}_nuc.csv",
        b_factor = "data/b_factor/{eq_class_members}_b_factor.csv"
    output:
        counts = "processed/counts/{eq_class_members}_counts.csv",
        h_bond = "processed/h_bond/{eq_class_members}_h_bond.csv",
        nuc = "processed/nuc/{eq_class_members}_nuc.csv",
        b_factor = "processed/b_factor/{eq_class_members}_b_factor.csv"
    log:
        stdout = "data/logs/process_data/{eq_class_members}.stdout",
        stderr = "data/logs/process_data/{eq_class_members}.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/process_data.py"

rule combine_data:
    input:
        counts = expand("processed/counts/{eq_class_members}_counts.csv", eq_class_members=eq_class_members),
        h_bond = expand("processed/h_bond/{eq_class_members}_h_bond.csv", eq_class_members=eq_class_members),
        nuc = expand("processed/nuc/{eq_class_members}_nuc.csv", eq_class_members=eq_class_members),
        b_factor = expand("processed/b_factor/{eq_class_members}_b_factor.csv", eq_class_members=eq_class_members)
    output:
        counts = "combined/counts.csv",
        h_bond = "combined/h_bond.csv",
        nuc = "combined/nuc.csv",
        b_factor = "combined/b_factor.csv"
    log:
        stdout = "data/logs/combine_data/output.stdout",
        stderr = "data/logs/combine_data/error.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/combine_data.py"

rule analyze_data:
    input:
        counts = "combined/counts.csv",
        h_bond = "combined/h_bond.csv",
        nuc = "combined/nuc.csv",
        b_factor = "combined/b_factor.csv"
    output:
        atom_pairs = "analysis/atom_pairs.csv",
        counts = "analysis/counts.csv"
    log:
        stdout = "data/logs/analyze_data/output.stdout",
        stderr = "data/logs/analyze_data/error.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/analyze_data.py"
