import scripts.parse_nrlist

configfile: "config/config.yaml"

# Prepare a list of strings where each string includes the equivalence class name, PDB ID, and chain info for the
# equivalence class members.
eq_class_members = scripts.parse_nrlist.parse_nrlist(config["rep_set_file"], config["all_members"])

rule all:
    input:
        "data/review_candidates/candidates.py",
        "data/review_amines/amines.py",
        "data/plots/pairs.png",
        "data/plots/density.png",
        "data/plots/nuc_id.png",
        "data/plots/acc_id.png",
        "data/plots/chi.png",
        "data/plots/dist.png"

rule collect_data:
    output:
        data = "data/collect_data/{eq_class_member}.csv"
    log:
        stdout = "misc/logs/collect_data/{eq_class_member}.stdout",
        stderr = "misc/logs/collect_data/{eq_class_member}.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/collect_data.py"

rule process_data:
    input:
        data = "data/collect_data/{eq_class_member}.csv"
    output:
        data = "data/process_data/{eq_class_member}.csv"
    log:
        stdout = "misc/logs/process_data/{eq_class_member}.stdout",
        stderr = "misc/logs/process_data/{eq_class_member}.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/process_data.py"

rule review_amines:
    input:
        data = expand("data/process_data/{eq_class_member}.csv", eq_class_member=eq_class_members)
    output:
        script = "data/review_amines/amines.py"
    log:
        stdout = "misc/logs/review_amines/output.stdout",
        stderr = "misc/logs/review_amines/error.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/review_amines.py"

rule identify_candidates:
    input:
        data = expand("data/process_data/{eq_class_member}.csv", eq_class_member=eq_class_members)
    output:
        candidates = "data/identify_candidates/candidates.csv"
    log:
        stdout = "misc/logs/identify_candidates/output.stdout",
        stderr = "misc/logs/identify_candidates/error.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/identify_candidates.py"

rule review_candidates:
    input:
        candidates = "data/identify_candidates/candidates.csv"
    output:
        script = "data/review_candidates/candidates.py"
    log:
        stdout = "misc/logs/review_candidates/output.stdout",
        stderr = "misc/logs/review_candidates/error.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/review_candidates.py"

rule combine_data:
    input:
        data = expand("data/process_data/{eq_class_member}.csv", eq_class_member=eq_class_members)
    output:
        combined = "data/combine_data/combined.csv",
        distances = "data/combine_data/distances.csv"
    log:
        stdout = "misc/logs/combine_data/output.stdout",
        stderr = "misc/logs/combine_data/error.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/combine_data.py"

rule create_plots:
    input:
        combined = "data/combine_data/combined.csv",
        distances = "data/combine_data/distances.csv"
    output:
        pairs = "data/plots/pairs.png",
        density = "data/plots/density.png",
        nuc_id = "data/plots/nuc_id.png",
        acc_id = "data/plots/acc_id.png",
        chi = "data/plots/chi.png",
        dist = "data/plots/dist.png"
    script:
        "scripts/create_plots.R"
