import scripts.parse_nrlist

configfile: "config/config.yaml"

# Prepare a list of strings where each string includes the equivalence class name, PDB ID, and chain info for the
# equivalence class members.
eq_class_members = scripts.parse_nrlist.parse_nrlist(config["rep_set_file"], config["all_members"])

rule all:
    input:
        "results/review_amines/amines.py",
        "results/plots/pairs.png",
        "results/plots/pseudotorsion_all.png",
        "results/plots/pseudotorsion_out.png",
        "results/plots/don_id.png",
        "results/plots/acc_pair_id.png",
        "results/plots/chi_partial.png",
        "results/plots/chi_partial_y.png",
        "results/plots/chi_combined.png",
        "results/plots/density_all.png",
        "results/plots/density_filtered.png",
        "results/plots/sasa_all_box.png",
        "results/plots/sasa_all_col.png",
        "results/plots/sasa_filtered_box.png"

rule collect_data:
    output:
        data = "results/collect_data/{eq_class_member}.csv"
    log:
        stdout = "results/logs/collect_data/{eq_class_member}.stdout",
        stderr = "results/logs/collect_data/{eq_class_member}.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/collect_data.py"

rule process_data:
    input:
        data = "results/collect_data/{eq_class_member}.csv"
    output:
        data = "results/process_data/{eq_class_member}.csv"
    log:
        stdout = "results/logs/process_data/{eq_class_member}.stdout",
        stderr = "results/logs/process_data/{eq_class_member}.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/process_data.py"

rule review_amines:
    input:
        data = expand("results/process_data/{eq_class_member}.csv", eq_class_member=eq_class_members)
    output:
        script = "results/review_amines/amines.py"
    log:
        stdout = "results/logs/review_amines/output.stdout",
        stderr = "results/logs/review_amines/error.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/review_amines.py"

rule combine_data:
    input:
        data = expand("results/process_data/{eq_class_member}.csv", eq_class_member=eq_class_members)
    output:
        combined = "results/combine_data/combined.csv"
    log:
        stdout = "results/logs/combine_data/output.stdout",
        stderr = "results/logs/combine_data/error.stderr"
    conda:
        "envs/general.yaml"
    script:
        "scripts/combine_data.py"

rule create_plots:
    input:
        combined = "results/combine_data/combined.csv"
    output:
        pairs = "results/plots/pairs.png",
        pseudotorsion_all = "results/plots/pseudotorsion_all.png",
        pseudotorsion_out = "results/plots/pseudotorsion_out.png",
        don_id = "results/plots/don_id.png",
        acc_pair_id = "results/plots/acc_pair_id.png",
        chi_partial = "results/plots/chi_partial.png",
        chi_partial_y = "results/plots/chi_partial_y.png",
        chi_combined = "results/plots/chi_combined.png",
        density_all = "results/plots/density_all.png",
        density_filtered = "results/plots/density_filtered.png",
        sasa_all_box= "results/plots/sasa_all_box.png",
        sasa_all_col= "results/plots/sasa_all_col.png",
        sasa_filtered_box = "results/plots/sasa_filtered_box.png"
    conda:
        "envs/general.yaml"
    script:
        "scripts/create_plots.R"
